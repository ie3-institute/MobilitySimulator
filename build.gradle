plugins {
  id 'groovy' // groovy support
  id 'java' // java support
  id 'scala' // scala support
  id 'pmd' // code check, working on source code
  id 'com.diffplug.spotless' version '8.1.0'//code format
  id "de.undercouch.download" version "5.6.0" // downloads plugin
  id "org.sonarqube" version "7.2.2.6593" // sonarqube
  id "org.scoverage" version "8.1" // scala code coverage scoverage
  id "com.github.maiflai.scalatest" version "0.33" // run scalatest without specific spec task
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id 'signing'
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
  id "com.github.johnrengelman.shadow" version "8.1.1"
  id "kr.motd.sphinx" version "2.10.1" // documentation generation
}

ext {
  javaVersion = JavaVersion.VERSION_21

  scalaVersion = '3'
  scalaBinaryVersion = '3.7.4'

  pekkoVersion = '1.4.0'
  slf4jVersion = '1.7.30'
  log4j2Version = '2.14.1'
  tscfgVersion = '1.2.5'
  scapegoatVersion = '3.2.4'

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins
}

apply from: scriptsLocation + 'tscfg.gradle' // config tasks
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'documentation.gradle' // documentation tasks + configuration
apply from: scriptsLocation + 'sonarqube.gradle' // sonarqube config
apply from: scriptsLocation + 'scoverage.gradle' // scoverage scala code coverage
apply from: scriptsLocation + 'uploadToMavenCentralPortal.gradle' // upload for deploy
apply from: scriptsLocation + 'stagingAtMavenCentralPortal.gradle' // stage for deploy
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'branchName.gradle'

repositories {
  mavenCentral() // searches in Sonatype's central repository
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' } // sonatype snapshot repo
}

dependencies {
  implementation('com.github.ie3-institute:PowerSystemUtils:3.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:PowerSystemDataModel:8.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }

  implementation 'com.github.ie3-institute:simonaAPI:0.12.0'

  implementation 'tech.units:indriya:2.2.3' // Physical units
  implementation "org.typelevel:squants_${scalaVersion}:1.8.3"
  implementation 'org.locationtech.jts.io:jts-io-common:1.20.0' // Location specific stuff
  implementation 'commons-io:commons-io:2.21.0' // I/O functionalities

  /* CORE Scala */
  implementation "org.scala-lang:scala3-library_3:${scalaBinaryVersion}"
  implementation "org.scala-lang.modules:scala-parallel-collections_${scalaVersion}:1.2.0"

  /* CORE pekko */
  testImplementation "org.apache.pekko:pekko-actor-typed_${scalaVersion}:${pekkoVersion}"

  /* Logging */
  implementation 'ch.qos.logback:logback-classic:1.5.25'
  implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:3.9.6" // pekko scala logging

  /* Scala compiler plugin for static code analysis */
  implementation "com.sksamuel.scapegoat:scalac-scapegoat-plugin_${scalaBinaryVersion}:${scapegoatVersion}"
  scalaCompilerPlugins("com.sksamuel.scapegoat:scalac-scapegoat-plugin_${scalaBinaryVersion}:${scapegoatVersion}")

  /* config */
  implementation 'com.typesafe:config:1.4.5'
  implementation "com.github.carueda:tscfg_${scalaVersion}:$tscfgVersion"
  implementation "com.github.scopt:scopt_${scalaVersion}:4.1.0" // cmd args parser

  /* Testing */
  testImplementation "org.scalatest:scalatest_$scalaVersion:3.2.19"
  testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.64.8' // scalatest html output
}

group = 'com.github.ie3-institute'
description = 'MobilitySimulator'

shadowJar {
  zip64 = true
}

// scapegoat hook configuration
// https://github.com/sksamuel/scapegoat
// using compileScala instead of tasks.withType(ScalaCompile) prevents applying scapegoat to scala test classes
// see https://docs.gradle.org/current/userguide/scala_plugin.html#sec:configure_scala_classpath for details
tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = [
    "-Xplugin:" + configurations.scalaCompilerPlugins.asPath,
    "-P:scapegoat:dataDir:" + rootDir + "/reports/scapegoat/src/",
    "-P:scapegoat:disabledInspections:ComparingFloatingPointTypes",
  ]
}

// separate scapegoat report for test classes
compileTestScala {
  scalaCompileOptions.additionalParameters = [
    "-Xplugin:" + configurations.scalaCompilerPlugins.asPath,
    "-P:scapegoat:dataDir:" + rootDir + "/reports/scapegoat/testsrc/",
    "-P:scapegoat:disabledInspections:ComparingFloatingPointTypes",
  ]
}